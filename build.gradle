plugins {
    id "groovy"
    id "net.ltgt.apt-eclipse" version "0.21"
    id "jacoco"
    id "checkstyle"
    id "codenarc"
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
}



version "0.1.0"
group "com.agorapulse"

repositories {
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

dependencies {
    annotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut:micronaut-validation"
    implementation platform("io.micronaut:micronaut-bom:$micronautVersion")
    implementation "io.micronaut:micronaut-http-client"
    implementation "io.micronaut:micronaut-inject"
    implementation "io.micronaut:micronaut-validation"
    implementation "io.micronaut:micronaut-runtime"
    implementation "io.micronaut:micronaut-http-server-netty"
    runtimeOnly "ch.qos.logback:logback-classic:1.2.3"
    testAnnotationProcessor platform("io.micronaut:micronaut-bom:$micronautVersion")
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testImplementation platform("io.micronaut:micronaut-bom:$micronautVersion")
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "io.micronaut.test:micronaut-test-junit5"
    testImplementation("org.spockframework:spock-core") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
    testImplementation "io.micronaut:micronaut-inject-groovy"
    testImplementation "io.micronaut.test:micronaut-test-spock"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.5.0"
}
test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
    options.compilerArgs.add('-parameters')
}

checkstyle {
    toolVersion = '8.23'
}

codenarc {
    toolVersion = '1.4'
}


// publishing
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        groovyMaven(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

// set bintrayUser & bintrayKey in gradle.properties
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['groovyMaven']
    publish = true

    final String projectName = project.name
    final String projectDescription = project.description

    pkg {
        websiteUrl = 'https://github.com/agorapulse/micronaut-rethrow'
        issueTrackerUrl = 'https://github.com/agorapulse/micronaut-rethrow/issues'
        vcsUrl = 'https://github.com/agorapulse/micronaut-rethrow.git'

        repo = 'libs' // or your repo name
        userOrg = 'agorapulse'
        name = projectName       // somehow project.* doesn't work in this closure
        desc = projectDescription
        licenses = ['Apache-2.0']
    }
    // dryRun = true // whether to run this as dry-run, without deploying
}